apply plugin: "java"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]


eclipse.project {
    name = appName + "-core"
}

task generateItems(type: GenerateTask) {
    fileMap = [
            'core/assets/dataItems/weapons.csv':'Weapon.class',
            'core/assets/dataItems/armors.csv':'Armor.class',
            'core/assets/dataItems/usableitems.csv':'UsableItem.class',
            'core/assets/dataItems/questitems.csv':'QuestItem.class',
            'core/assets/dataItems/miscellaneousitems.csv':'MiscellaneousItem.class'
    ]
    preamble = { classFile ->
        classFile.append 'package com.mygdx.game.Items;\n'
        classFile.append 'import com.mygdx.game.Items.types.Armor;\n'
        classFile.append 'import com.mygdx.game.Items.types.Weapon;\n'
        classFile.append 'import com.mygdx.game.Items.types.QuestItem;\n'
        classFile.append 'import com.mygdx.game.Items.types.UsableItem;\n'
        classFile.append 'import com.mygdx.game.Items.types.MiscellaneousItem;\n'
        classFile.append '\n'
        classFile.append 'public enum ItemType {\n'
    }
    epilogue = { classFile ->
        classFile.append '    private final Class<? extends Item> cls;\n'
        classFile.append '\n'
        classFile.append '    private ItemType(Class<? extends Item> cls) {\n'
        classFile.append '        this.cls = cls;\n'
        classFile.append '    }\n'
        classFile.append '\n'
        classFile.append '    public Class<? extends Item> getCls() {\n'
        classFile.append '        return cls;\n'
        classFile.append '    }'
        classFile.append '\n}'
    }
    outputFile = 'core\\src\\com\\mygdx\\game\\Items\\ItemType.java'
}

task generateEntities(type: GenerateTask, dependsOn: generateItems) {
    fileMap = [
            'core/assets/dataEntities/enemies.csv':'Enemy.class'
    ]
    preamble = { classFile ->
        classFile.append 'package com.mygdx.game.Entities;\n'
        classFile.append 'import com.mygdx.game.Entities.NonStatics.Enemy;\n'
        classFile.append '\n'
        classFile.append 'public enum EntityType {\n'
    }
    epilogue = { classFile ->
        classFile.append '    private final Class<? extends Entity> cls;\n'
        classFile.append '\n'
        classFile.append '    private EntityType(Class<? extends Entity> cls) {\n'
        classFile.append '        this.cls = cls;\n'
        classFile.append '    }\n'
        classFile.append '\n'
        classFile.append '    public Class<? extends Entity> getCls() {\n'
        classFile.append '        return cls;\n'
        classFile.append '    }'
        classFile.append '\n}'
    }
    outputFile = 'core\\src\\com\\mygdx\\game\\Entities\\EntityType.java'
}

class GenerateTask extends DefaultTask {

    def fileMap
    def preamble
    def epilogue
    def outputFile

    @TaskAction
    def runTask() {
        def classFile = new StringBuilder()
        preamble classFile

        def actualEnums = []
        fileMap.each { k, v ->
            def file = new File(k).text
            def enums = file.split("\\n").collect { l ->
                def line = l.split ','
                line[0].toUpperCase().replace ' ', '_'
            }

            actualEnums += enums.collect { e ->
                "\t" + e + '(' + v + ')'
            }
        }
        classFile.append actualEnums.join(',\n')
        classFile.append ';\n\n'

        epilogue classFile

        def contents = classFile.toString()

        def srcFile = new File(outputFile)
        srcFile.write contents
    }

}
